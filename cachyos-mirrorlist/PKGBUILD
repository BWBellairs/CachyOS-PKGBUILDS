# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgbase=cachyos-mirrorlist
pkgname=('cachyos-mirrorlist' 'cachyos-v3-mirrorlist' 'cachyos-v4-mirrorlist')
pkgver=24
pkgrel=1
pkgdesc="CachyOS repository mirrorlist"
arch=('any')
license=(GPL-3.0-or-later)
groups=(cachyos)
source=(cachyos-mirrorlist)
sha512sums=('5f8246752ebecb0ec45e11c7dd445649d7c27caba8052b6a3ddbdf4e12be09f01c1909ec88014e297caa06b7bd2d6061cd8bff27b59293314cfe8eea5a8b5b4a')

package_cachyos-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v3-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v3/|g' "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v4-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v4/|g' "$pkgdir/etc/pacman.d/$pkgname"
}
