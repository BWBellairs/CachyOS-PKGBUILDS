# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgbase=cachyos-mirrorlist
pkgname=('cachyos-mirrorlist' 'cachyos-v3-mirrorlist' 'cachyos-v4-mirrorlist')
pkgver=24
pkgrel=1
pkgdesc="CachyOS repository mirrorlist"
arch=('any')
license=(GPL-3.0-or-later)
groups=(cachyos)
source=(cachyos-mirrorlist)
sha512sums=('be9b03db0285158343d96c7ad94e237d3f8080e5aa380b59ca29d1a560d3f32abae27026f0a5c17d9b5fc275fc2e82b240c336ae5780d82596bd692dea56ba1c')

package_cachyos-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v3-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v3/|g' "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v4-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v4/|g' "$pkgdir/etc/pacman.d/$pkgname"
}
