# Maintainer: Konstantin Rannev <konstantin.rannev@gmail.com>
# Contributor: Ash <xash at riseup d0t net>

pkgname=lsfg-vk
pkgver=r103.223f4ba
pkgrel=1
pkgdesc="Lossless Scaling Frame Generation on Linux via DXVK/Vulkan"
arch=('x86_64')
url="https://github.com/PancakeTAS/lsfg-vk"
license=('MIT')
depends=('vulkan-icd-loader')
makedepends=('clang' 'llvm' 'vulkan-headers' 'cmake' 'meson' 'ninja' 'git' 'sed' 'sdl2' 'glslang')
source=('git+https://github.com/PancakeTAS/lsfg-vk#commit=3ec418fd0f802f245bc31511d33c94a25d045ae8')
sha256sums=('1dfe424dcd9a551d41b7021108315129d3ee44113c7e60f04b22ecbaaaaf2889')

pkgver() {
	cd "$srcdir/${pkgname}"

	# Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname}"

	cmake -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DCMAKE_CXX_CLANG_TIDY="" \
    -DCMAKE_SHARED_LINKER_FLAGS="-Wl,-z,lazy" # fixes makepkg's default "-z,now" flag which strips out the necessary symbols
    cmake --build build
}

package() {
	cd "$srcdir/${pkgname}"

	DESTDIR="$pkgdir/" cmake --install build --prefix=/usr
}
