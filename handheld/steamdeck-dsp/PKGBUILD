# Maintainer Ethan Geller (ethang@valvesoftware.com)

pkgname=steamdeck-dsp
pkgver=0.68
pkgrel=1
arch=('any')
url="https://gitlab.com/evlaV/valve-hardware-audio-processing"
pkgdesc="Steamdeck Audio Processing"
license=('GPL-2.0-or-later')
depends=('pipewire')
makedepends=('git' 'openssh' 'base-devel' 'glibc' 'faust' 'ladspa' 'lv2' 'boost' 'linux-api-headers' 'qt5-base')
source=("git+https://gitlab.com/evlaV/valve-hardware-audio-processing.git#tag=${pkgver}")
sha512sums=('298ea6db0af20a6a3207d870e892440c03e6607eaae3ea87eb361c36fc2c44be81e1b185deb734a82c086e733bc302106d20ec6e4409ba434699ef183b0a62ae')

build() {
  cd valve-hardware-audio-processing
  make FAUSTINC="/usr/include/faust" FAUSTLIB="/usr/share/faust"
}

package() {
  cd valve-hardware-audio-processing
  make DEST_DIR="$pkgdir/" install
}
