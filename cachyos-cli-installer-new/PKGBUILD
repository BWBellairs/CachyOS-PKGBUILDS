# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-cli-installer-new
_pkgname=New-Cli-Installer
pkgver=0.9.1
pkgrel=1
pkgdesc='New CLI installer for CachyOS'
arch=('x86_64')
license=(GPL-3.0-or-later)
url="https://github.com/cachyos/new-cli-installer"
depends=('fzf' 'gawk' 'chwd' 'cachyos-rate-mirrors' 'curl' 'util-linux')
makedepends=('cmake' 'ninja' 'git')
source=("$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('1be0b22d385fd0c3044c9cf5448a684585f53117e2648152b6fbef287e4ce903499ba6725826a9c169a94c5ee48a06d2665ea0cae83341dd1b532187c5907734')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DENABLE_DEVENV=OFF \
        -DCOS_BUILD_STATIC=ON \
        -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake --build build --parallel $_cpuCount
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  DESTDIR="${pkgdir}" cmake --build build --target install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 net-profiles.toml "${pkgdir}/var/lib/cachyos-installer/net-profiles.toml"
}

# vim:set sw=2 sts=2 et:
